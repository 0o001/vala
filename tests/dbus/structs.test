Packages: dbus-glib-1

Program: client

struct FooStruct {
	int i;
	string s;

	public FooStruct (int i, string s) {
		this.i = i;
		this.s = s;
	}
}

[DBus (name = "org.example.Test")]
interface Test : Object {
	public abstract FooStruct test_property { owned get; set; }

	public abstract FooStruct test_struct (FooStruct f, out FooStruct g) throws DBus.Error;
}

void main () {
	var conn = DBus.Bus.get (DBus.BusType.SESSION);

	// client
	var test = (Test) conn.get_object ("org.example.Test", "/org/example/test");

	FooStruct f, g, h;
	f = FooStruct (42, "hello");
	h = test.test_struct (f, out g);
	assert (g.i == 23);
	assert (g.s == "world");
	assert (h.i == 11);
	assert (h.s == "vala");

	test.test_property = f;
	g = test.test_property;
	assert (g.i == 42);
	assert (g.s == "hello");
}

Program: server

struct FooStruct {
	int i;
	string s;

	public FooStruct (int i, string s) {
		this.i = i;
		this.s = s;
	}
}

[DBus (name = "org.example.Test")]
class Test : Object {
	public FooStruct test_property { owned get; set; }

	public FooStruct test_struct (FooStruct f, out FooStruct g) {
		assert (f.i == 42);
		assert (f.s == "hello");
		g = FooStruct (23, "world");
		return FooStruct (11, "vala");
	}
}

MainLoop main_loop;

void client_exit (Pid pid, int status) {
	// client finished, terminate server
	assert (status == 0);
	main_loop.quit ();
}

void main () {
	var conn = DBus.Bus.get (DBus.BusType.SESSION);
	dynamic DBus.Object bus = conn.get_object ("org.freedesktop.DBus", "/org/freedesktop/DBus", "org.freedesktop.DBus");

	// try to register service in session bus
	uint request_name_result = bus.request_name ("org.example.Test", (uint) 0);
	assert (request_name_result == DBus.RequestNameReply.PRIMARY_OWNER);

	// start server
	var server = new Test ();
	conn.register_object ("/org/example/test", server);

	// server ready, spawn client
	Pid client_pid;
	Process.spawn_async (null, { "test", "/dbus/structs/client" }, null, SpawnFlags.DO_NOT_REAP_CHILD, null, out client_pid);
	ChildWatch.add (client_pid, client_exit);

	main_loop = new MainLoop (null, false);
	main_loop.run ();
}
